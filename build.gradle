plugins {
	id 'java'
	id 'org.springframework.boot' version '3.2.7' apply false
	id 'io.spring.dependency-management' version '1.1.5' apply false
}

ext {
	springBootVersion = '3.2.7'
	javaVersion = '17'
	lombokVersion = '1.18.30'  // Lombok 버전 명시 (예시)
	springDocVersion = '2.1.0'
}

allprojects {
	group = 'com.subride'
	version = '0.0.1-SNAPSHOT'

	repositories {
		mavenCentral()
	}
}

subprojects {
	apply plugin: 'java'
	apply plugin: 'org.springframework.boot'
	apply plugin: 'io.spring.dependency-management'

	sourceCompatibility = javaVersion
	targetCompatibility = javaVersion

	dependencies {
		implementation 'org.springframework.boot:spring-boot-starter-actuator'
		implementation 'org.springframework.boot:spring-boot-starter-web'
		implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:${springDocVersion}"
		implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

		compileOnly "org.projectlombok:lombok:${lombokVersion}"
		annotationProcessor "org.projectlombok:lombok:${lombokVersion}"

		testImplementation 'org.springframework.boot:spring-boot-starter-test'
		testImplementation 'org.mockito:mockito-core'
		testImplementation 'org.mockito:mockito-junit-jupiter'
		testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	}

	test {
		useJUnitPlatform()
	}
}

project(':common') {
	bootJar { enabled = false }
	jar { enabled = true }
}

project(':domain-database') {
	bootJar { enabled = false }
	jar { enabled = true }
}

project(':app-category') {
	apply plugin: 'application'

	dependencies {
		implementation project(':domain-database')
		implementation project(':common')
	}

	bootJar {
		enabled = true
		mainClass = 'com.category.brand.Application'
	}

	jar { enabled = false }

	application {
		mainClass = 'com.category.brand.Application'
	}
}